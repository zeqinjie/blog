---
title: FlutterEngineGroup å¼•æ“å¯åŠ¨æµç¨‹ï¼ˆiOSï¼‰
date: 2023-04-02 16:08:42
categories: "Flutter"
tags:
	- Flutter
---

## æµç¨‹å›¾

![](https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/3132df83ca814d1b8b074b50a0f994c2~tplv-k3u1fbpfcp-zoom-1.image)

## åˆ›å»º FlutterEngineGroup

### Flutter å¼•æ“ç»„çš„ä½œç”¨

- 1ã€ä¸»è¦ç”¨äºç®¡ç†å¤šä¸ª Flutter å¼•æ“ï¼Œä» FlutterEngineGroup ç”Ÿæˆçš„ FlutterEngine å…·æœ‰å¸¸ç”¨å…±äº«èµ„æºï¼ˆä¾‹å¦‚ GPU ä¸Šä¸‹æ–‡ã€å­—ä½“åº¦é‡å’Œéš”ç¦»çº¿ç¨‹çš„å¿«ç…§ï¼‰çš„æ€§èƒ½ä¼˜åŠ¿ï¼Œä»è€ŒåŠ å¿«é¦–æ¬¡æ¸²æŸ“çš„é€Ÿåº¦ã€é™ä½å»¶è¿Ÿå¹¶é™ä½å†…å­˜å ç”¨ã€‚
- 2ã€å®˜æ–¹è¯´ï¼šé‡‡ç”¨ Group ç®¡ç†å¤šå¼•æ“åœ¨å†…å­˜ä¸Šï¼Œé™¤äº†ç¬¬ä¸€ä¸ª Engine å¯¹è±¡ä¹‹å¤–ï¼Œåç»­æ¯ä¸ª Engine å¯¹è±¡åœ¨ Android å’Œ iOS ä¸Šä»…å ç”¨ 180kB ã€‚â€œå®é™…ä¸Šæ˜¯å¦ï¼Œå¾…éªŒè¯â€ ã€‚
- 3ã€ä¾‹å­ let engines = FlutterEngineGroup(name: "multiple-flutters", project: nil)

### FlutterEngineGroup æºç 

- ä»æºç ä¸Šçœ‹ FlutterEngineGroup ç±»ä¸»è¦æœ‰ä¸€ä¸ª engines æ•°ç»„ï¼Œå»ç»´æŠ¤ç®¡ç†è¿™äº›å¼•æ“

```
@interface FlutterEngineGroup ()
@property(nonatomic, copy) NSString* name;
@property(nonatomic, retain) NSMutableArray<NSValue*>* engines;
// å¯¹è±¡æ¥æŒ‡å®šå…¥å£æ–‡ä»¶å’Œ Assets
@property(nonatomic, retain) FlutterDartProject* project;
@end

@implementation FlutterEngineGroup {
  int _enginesCreatedCount;
}

/// åˆ›å»º FlutterEngineGroup ,å†…éƒ¨åˆå§‹åŒ–æ•°ç»„
- (instancetype)initWithName:(NSString*)name project:(nullable FlutterDartProject*)project {
  self = [super init];
  if (self) {
    _name = [name copy];
    _engines = [[NSMutableArray<NSValue*> alloc] init];
    _project = [project retain];
  }
  return self;
}
...
```

## åˆ›å»º FlutterEngine

### 1ã€makeEngineWithOptions

- 1ã€å¼•æ“åˆ›å»ºé€»è¾‘ï¼Œå½“å¼•æ“æ± æ²¡æœ‰å¼•æ“æ—¶ï¼Œé€šè¿‡`makeEngine`åˆ›å»ºä¸€ä¸ªæ–°å¼•æ“ã€‚

- 2ã€å¦‚æœå¼•æ“æ± é‡Œæœ‰å¼•æ“ï¼Œåˆ™è¿”å›å½“å‰å¼•æ“ï¼Œå¹¶ä¸”é€šè¿‡ `spawnWithEntrypoint` æ–¹æ³•è®¾ç½®ç›¸åŒçš„å…¥å£ç‚¹ã€dart åº“è·¯å¾„ã€è·¯ç”±ã€å…¥å£å‚æ•°ã€‚
    - aã€FlutterEngineGroup æ˜¯æ€ä¹ˆåšåˆ°éƒ¨åˆ†çš„å¼•æ“èµ„æºå…±äº«å‘¢ï¼Ÿé‡ç‚¹åœ¨äº `spawnWithEntrypoint` å†…éƒ¨å®ç°ï¼Œåé¢æˆ‘ä»¬å†è¯´~

- 3ã€ä¾‹å­ï¼š let newEngine = appDelegate.engines.makeEngine(with: options)

- 4ã€makeEngineWithOptions æºç å¦‚ä¸‹ï¼š

```
/// åˆ›å»ºå¹¶è¿”å›å¼•æ“
- (FlutterEngine*)makeEngineWithOptions:(nullable FlutterEngineGroupOptions*)options {
  NSString* entrypoint = options.entrypoint;
  NSString* libraryURI = options.libraryURI;
  NSString* initialRoute = options.initialRoute;
  NSArray<NSString*>* entrypointArgs = options.entrypointArgs;

  FlutterEngine* engine;
  // å½“å¼•æ“æ²¡æœ‰åˆ›å»ºè¿‡ï¼Œé‚£ä¹ˆå°±åˆ›å»ºä¸€ä¸ªæ–°çš„å¼•æ“
  if (self.engines.count <= 0) {
    engine = [self makeEngine];
    [engine runWithEntrypoint:entrypoint
                   libraryURI:libraryURI
                 initialRoute:initialRoute
               entrypointArgs:entrypointArgs];
  } else {
    // å·²ç»å­˜åœ¨å¼•æ“ï¼Œåˆ™è·å–å½“å‰å¼•æ“ï¼Œå¹¶é€šè¿‡ spawnWithEntrypoint æ–¹æ³•åšä¸€ç³»åˆ—æ“ä½œ
    FlutterEngine* spawner = (FlutterEngine*)[self.engines[0] pointerValue];
    engine = [spawner spawnWithEntrypoint:entrypoint
                               libraryURI:libraryURI
                             initialRoute:initialRoute
                           entrypointArgs:entrypointArgs];
  }
  [_engines addObject:[NSValue valueWithPointer:engine]];

  NSNotificationCenter* center = [NSNotificationCenter defaultCenter];
  [center addObserver:self
             selector:@selector(onEngineWillBeDealloced:)
                 name:kFlutterEngineWillDealloc
               object:engine];

  return engine;
}

```

### 2ã€makeEngine

-   makeEngine çš„æºç 

```
// @property(nonatomic, readonly) NSMutableDictionary* pluginPublications;
// @property(nonatomic, readonly) NSMutableDictionary<NSString*, FlutterEngineRegistrar*>* registrars;


/// åˆ›å»ºæ–°çš„å¼•æ“
- (FlutterEngine*)makeEngine {
  NSString* engineName = [NSString stringWithFormat:@"%@.%d", self.name, ++_enginesCreatedCount];
  FlutterEngine* result = [[FlutterEngine alloc] initWithName:engineName project:self.project];
  return [result autorelease];
}

/// å®ç°é€»è¾‘
- (instancetype)initWithName:(NSString*)labelPrefix
                     project:(FlutterDartProject*)project
      allowHeadlessExecution:(BOOL)allowHeadlessExecution
          restorationEnabled:(BOOL)restorationEnabled {
  self = [super init];
  NSAssert(self, @"Super init cannot be nil");
  NSAssert(labelPrefix, @"labelPrefix is required");

  _restorationEnabled = restorationEnabled;
  _allowHeadlessExecution = allowHeadlessExecution;
  _labelPrefix = [labelPrefix copy];

  _weakFactory = std::make_unique<fml::WeakPtrFactory<FlutterEngine>>(self);

  if (project == nil) {
    _dartProject.reset([[FlutterDartProject alloc] init]);
  } else {
    _dartProject.reset([project retain]);
  }

  if (!EnableTracingIfNecessary([_dartProject.get() settings])) {
    NSLog(
        @"Cannot create a FlutterEngine instance in debug mode without Flutter tooling or "
        @"Xcode.\n\nTo launch in debug mode in iOS 14+, run flutter run from Flutter tools, run "
        @"from an IDE with a Flutter IDE plugin or run the iOS project from Xcode.\nAlternatively "
        @"profile and release mode apps can be launched from the home screen.");
    [self release];
    return nil;
  }

  _pluginPublications = [[NSMutableDictionary alloc] init];
  _registrars = [[NSMutableDictionary alloc] init];
  [self recreatePlatformViewController];

  _binaryMessenger = [[FlutterBinaryMessengerRelay alloc] initWithParent:self];
  _textureRegistry = [[FlutterTextureRegistryRelay alloc] initWithParent:self];
  _connections.reset(new flutter::ConnectionCollection());

  NSNotificationCenter* center = [NSNotificationCenter defaultCenter];
  [center addObserver:self
             selector:@selector(onMemoryWarning:)
                 name:UIApplicationDidReceiveMemoryWarningNotification
               object:nil];

  [center addObserver:self
             selector:@selector(applicationWillEnterForeground:)
                 name:UIApplicationWillEnterForegroundNotification
               object:nil];

  [center addObserver:self
             selector:@selector(applicationDidEnterBackground:)
                 name:UIApplicationDidEnterBackgroundNotification
               object:nil];

  [center addObserver:self
             selector:@selector(onLocaleUpdated:)
                 name:NSCurrentLocaleDidChangeNotification
               object:nil];

  return self;
}
```

#### aã€åˆå§‹åŒ– FlutterDartProject å¯¹è±¡

- 1ã€ç”¨äº Flutter å¼•æ“ä¸­è¿è¡Œ Dart ä»£ç ã€‚Flutter å¼•æ“é€šè¿‡ FlutterDartProject ä¸ Dart ä»£ç è¿›è¡Œäº¤äº’ï¼Œæ¯”å¦‚å¯åŠ¨ Dart VMï¼ŒåŠ è½½ Dart åº“ï¼Œæ‰§è¡Œ Dart å‡½æ•°ç­‰ç­‰ã€‚åœ¨ Flutter åº”ç”¨ç¨‹åºä¸­ï¼Œé€šå¸¸ä¼šåˆ›å»ºä¸€ä¸ª FlutterDartProject å®ä¾‹ç”¨äºç®¡ç† Dart ä»£ç çš„æ‰§è¡Œã€‚
- 2ã€åˆå§‹åŒ– _settings ï¼Œä»æŒ‡å®šçš„ bundle ä¸­è¯»å– FlutterSettings é…ç½®ä¿¡æ¯ï¼Œå¹¶è¿”å›ä¸€ä¸ª FlutterSettings å¯¹è±¡ã€‚è¿™ä¸ªå¯¹è±¡åŒ…å«äº†åº”ç”¨çš„å„ç§é…ç½®ä¿¡æ¯ï¼Œä¾‹å¦‚å¼•æ“çš„ç‰ˆæœ¬ã€æ˜¯å¦å¯ç”¨ Dart å¼€å‘è€…å·¥å…·ã€æ˜¯å¦æ”¯æŒçƒ­é‡è½½ç­‰ã€‚

```
struct Settings {
    Settings();
    ...
    // Enable the Impeller renderer on supported platforms. Ignored if Impeller is
    // not supported on the platform.
    bool enable_impeller = false;
}

- (instancetype)init {
  return [self initWithPrecompiledDartBundle:nil];
}

- (instancetype)initWithPrecompiledDartBundle:(nullable NSBundle*)bundle {
  self = [super init];

  if (self) {
    _settings = FLTDefaultSettingsForBundle(bundle);
  }

  return self;
}

flutter::Settings FLTDefaultSettingsForBundle(NSBundle* bundle) {
    auto command_line = flutter::CommandLineFromNSProcessInfo();

    // Precedence:
    // 1. Settings from the specified NSBundle.
    // 2. Settings passed explicitly via command-line arguments.
    // 3. Settings from the NSBundle with the default bundle ID.
    // 4. Settings from the main NSBundle and default values.

    NSBundle* mainBundle = [NSBundle mainBundle];
    NSBundle* engineBundle = [NSBundle bundleForClass:[FlutterViewController class]];

    bool hasExplicitBundle = bundle != nil;
    if (bundle == nil) {
        bundle = [NSBundle bundleWithIdentifier:[FlutterDartProject defaultBundleIdentifier]];
    }
    if (bundle == nil) {
        bundle = mainBundle;
    }

    auto settings = flutter::SettingsFromCommandLine(command_line);

    settings.task_observer_add = [](intptr_t key, const fml::closure& callback) {
        fml::MessageLoop::GetCurrent().AddTaskObserver(key, callback);
    };

    settings.task_observer_remove = [](intptr_t key) {
        fml::MessageLoop::GetCurrent().RemoveTaskObserver(key);
    };

    settings.log_message_callback = [](const std::string& tag, const std::string& message) {
        // TODO(cbracken): replace this with os_log-based approach.
        // https://github.com/flutter/flutter/issues/44030
        std::stringstream stream;
        if (!tag.empty()) {
            stream << tag << ": ";
        }
        stream << message;
        std::string log = stream.str();
        syslog(LOG_ALERT, "%.*s", (int)log.size(), log.c_str());
    };

    // The command line arguments may not always be complete. If they aren't, attempt to fill in
    // defaults.

    // Flutter ships the ICU data file in the bundle of the engine. Look for it there.
    if (settings.icu_data_path.empty()) {
        NSString* icuDataPath = [engineBundle pathForResource:@"icudtl" ofType:@"dat"];
        if (icuDataPath.length > 0) {
            settings.icu_data_path = icuDataPath.UTF8String;
        }
    }

    if (flutter::DartVM::IsRunningPrecompiledCode()) {
        if (hasExplicitBundle) {
            NSString* executablePath = bundle.executablePath;
            if ([[NSFileManager defaultManager] fileExistsAtPath:executablePath]) {
                settings.application_library_path.push_back(executablePath.UTF8String);
            }
        }

        // No application bundle specified.  Try a known location from the main bundle's Info.plist.
        if (settings.application_library_path.empty()) {
            NSString* libraryName = [mainBundle objectForInfoDictionaryKey:@"FLTLibraryPath"];
            NSString* libraryPath = [mainBundle pathForResource:libraryName ofType:@""];
            if (libraryPath.length > 0) {
                NSString* executablePath = [NSBundle bundleWithPath:libraryPath].executablePath;
                if (executablePath.length > 0) {
                    settings.application_library_path.push_back(executablePath.UTF8String);
                }
            }
        }

        // In case the application bundle is still not specified, look for the App.framework in the
        // Frameworks directory.
        if (settings.application_library_path.empty()) {
            NSString* applicationFrameworkPath = [mainBundle pathForResource:@"Frameworks/App.framework"
                                      ofType:@""];
            if (applicationFrameworkPath.length > 0) {
                NSString* executablePath =
                    [NSBundle bundleWithPath:applicationFrameworkPath].executablePath;
                if (executablePath.length > 0) {
                    settings.application_library_path.push_back(executablePath.UTF8String);
                }
            }
        }
    }

    // Checks to see if the flutter assets directory is already present.
    if (settings.assets_path.empty()) {
        NSString* assetsName = [FlutterDartProject flutterAssetsName:bundle];
        NSString* assetsPath = [bundle pathForResource:assetsName ofType:@""];

        if (assetsPath.length == 0) {
            assetsPath = [mainBundle pathForResource:assetsName ofType:@""];
        }

        if (assetsPath.length == 0) {
            NSLog(@"Failed to find assets path for "%@"", assetsName);
        } else {
            settings.assets_path = assetsPath.UTF8String;

            // Check if there is an application kernel snapshot in the assets directory we could
            // potentially use.  Looking for the snapshot makes sense only if we have a VM that can use
            // it.
            if (!flutter::DartVM::IsRunningPrecompiledCode()) {
                NSURL* applicationKernelSnapshotURL =
                    [NSURL URLWithString:@(kApplicationKernelSnapshotFileName)
 relativeToURL:[NSURL fileURLWithPath:assetsPath]];
                if ([[NSFileManager defaultManager] fileExistsAtPath:applicationKernelSnapshotURL.path]) {
                    settings.application_kernel_asset = applicationKernelSnapshotURL.path.UTF8String;
                } else {
                    NSLog(@"Failed to find snapshot: %@", applicationKernelSnapshotURL.path);
                }
            }
        }
    }

    // Domain network configuration
    // Disabled in https://github.com/flutter/flutter/issues/72723.
    // Re-enable in https://github.com/flutter/flutter/issues/54448.
    settings.may_insecurely_connect_to_all_domains = true;
    settings.domain_network_policy = "";

    // Whether to enable Impeller.
    NSNumber* nsEnableWideGamut = [mainBundle objectForInfoDictionaryKey:@"FLTEnableWideGamut"];
    // TODO(gaaclarke): Make this value `on` by default (pending memory audit).
    BOOL enableWideGamut = nsEnableWideGamut ? nsEnableWideGamut.boolValue : NO;
    settings.enable_wide_gamut = enableWideGamut;

    // Whether to enable Impeller.
    NSNumber* enableImpeller = [mainBundle objectForInfoDictionaryKey:@"FLTEnableImpeller"];
    // Change the default only if the option is present.
    if (enableImpeller != nil) {
        settings.enable_impeller = enableImpeller.boolValue;
    }

    NSNumber* enableTraceSystrace = [mainBundle objectForInfoDictionaryKey:@"FLTTraceSystrace"];
    // Change the default only if the option is present.
    if (enableTraceSystrace != nil) {
        settings.trace_systrace = enableTraceSystrace.boolValue;
    }

    NSNumber* enableDartProfiling = [mainBundle objectForInfoDictionaryKey:@"FLTEnableDartProfiling"];
    // Change the default only if the option is present.
    if (enableDartProfiling != nil) {
        settings.enable_dart_profiling = enableDartProfiling.boolValue;
    }

    // Leak Dart VM settings, set whether leave or clean up the VM after the last shell shuts down.
    NSNumber* leakDartVM = [mainBundle objectForInfoDictionaryKey:@"FLTLeakDartVM"];
    // It will change the default leak_vm value in settings only if the key exists.
    if (leakDartVM != nil) {
        settings.leak_vm = leakDartVM.boolValue;
    }

    #if FLUTTER_RUNTIME_MODE == FLUTTER_RUNTIME_MODE_DEBUG
    // There are no ownership concerns here as all mappings are owned by the
    // embedder and not the engine.
    auto make_mapping_callback = [](const uint8_t* mapping, size_t size) {
        return [mapping, size]() { return std::make_unique<fml::NonOwnedMapping>(mapping, size); };
    };

    settings.dart_library_sources_kernel =
        make_mapping_callback(kPlatformStrongDill, kPlatformStrongDillSize);
    #endif  // FLUTTER_RUNTIME_MODE == FLUTTER_RUNTIME_MODE_DEBUG

    // If we even support setting this e.g. from the command line or the plist,
    // we should let the user override it.
    // Otherwise, we want to set this to a value that will avoid having the OS
    // kill us. On most iOS devices, that happens somewhere near half
    // the available memory.
    // The VM expects this value to be in megabytes.
    if (settings.old_gen_heap_size <= 0) {
        settings.old_gen_heap_size = std::round([NSProcessInfo processInfo].physicalMemory * .48 /
                                        flutter::kMegaByteSizeInBytes);
    }

    // This is the formula Android uses.
    // https://android.googlesource.com/platform/frameworks/base/+/39ae5bac216757bc201490f4c7b8c0f63006c6cd/libs/hwui/renderthread/CacheManager.cpp#45
    CGFloat scale = [UIScreen mainScreen].scale;
    CGFloat screenWidth = [UIScreen mainScreen].bounds.size.width * scale;
    CGFloat screenHeight = [UIScreen mainScreen].bounds.size.height * scale;
    settings.resource_cache_max_bytes_threshold = screenWidth * screenHeight * 12 * 4;

    return settings;
}

```

##### Flutter 3.9.0 ç‰ˆæ‰é»˜è®¤æ¸²æŸ“å¼•æ“ skia or impeller ? æˆ‘å¯ä»¥ä¿®æ”¹å…³é—­é»˜è®¤ skia æ¸²æŸ“æ¨¡å¼å—? ä»¥åŠå…¶ä»–é»˜è®¤é…ç½®è°ƒæ•´å—ï¼Ÿæˆ‘ä»¬çœ‹çœ‹ flutter::Settings é»˜è®¤è®¾ç½®çš„æºç ã€‚

- ä»æºç è¯»åˆ°å‘ç° mainBundle key FLTEnableImpeller æ˜¯è®¾ç½® impeller æ¸²æŸ“æ¨¡å¼ã€‚æˆªå›¾é»˜è®¤è®¾ç½® YESï¼Œæ‰€ä»¥æˆ‘ä»¬ä¹Ÿå¯ä»¥å…³é—­ impeller é‡‡ç”¨ skia æ¸²æŸ“å¼•æ“~

![iShot_2023-04-01_21.56.15.png](https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/9bc6ce2414174d4a836306ebacf0cbbb~tplv-k3u1fbpfcp-zoom-1.image)

- 1ã€Flutter 3.9.0 ç‰ˆæœ¬ä¸­å‘ç°ï¼Œé»˜è®¤Flutter Engineä¸­èµ„æºç¼“å­˜æ‰€å ç”¨çš„æœ€å¤§å†…å­˜å¤§å°å…¶å®æ˜¯æŒ‰å±å¹•å¤§å°åŠ¨æ€è°ƒæ•´çš„~å¾ˆç¥å¥‡å§~~
- 2ã€å› æ­¤æˆ‘ä»¬é˜…è¯»æºç å»å‘ç° Flutter å¼•æ“ä¸€äº›é»˜è®¤é…ç½®~

#### bã€åˆå§‹åŒ–æ’ä»¶è¡¨ _pluginPublications ä¸ _registrars

- 1ã€_pluginPublicationsï¼šæ¯å½“ä¸€ä¸ªæ’ä»¶åœ¨ FlutterEngine ä¸­æ³¨å†Œæ—¶ï¼Œ key å¯¹åº”çš„ value ä¼šè¢«è®¾ç½® [NSNull null]ï¼Œå¹¶å°†ä¸€ä¸ª Flutter æ’ä»¶æ³¨å†Œåˆ° registrar ä¸­ï¼Œå‘å¸ƒåˆ° Flutter æ’ä»¶åº“ä¸­ã€‚
- 2ã€_registrars: ç®¡ç†æ’ä»¶çš„æ³¨å†Œè¡¨ï¼Œå­˜å‚¨çš„æ˜¯ FlutterPluginRegistrar æ’ä»¶å®ä¾‹å¯¹è±¡ã€‚
- 3ã€æºç å¯ä»¥æŸ¥çœ‹æ³¨å†Œæ’ä»¶çš„é€»è¾‘å¦‚ä¸‹

```
- (void)publish:(NSObject*)value {
  _flutterEngine.pluginPublications[_pluginKey] = value;
}
...   
- (NSObject<FlutterPluginRegistrar>*)registrarForPlugin:(NSString*)pluginKey {
  NSAssert(self.pluginPublications[pluginKey] == nil, @"Duplicate plugin key: %@", pluginKey);
  self.pluginPublications[pluginKey] = [NSNull null];
  FlutterEngineRegistrar* result = [[FlutterEngineRegistrar alloc] initWithPlugin:pluginKey
                                                                    flutterEngine:self];
  self.registrars[pluginKey] = result;
  return [result autorelease];
}
```

- ä¾‹å¦‚æ³¨å†Œ dart-native æ’ä»¶ ï¼Œregistry æ˜¯å½“å‰å¼•æ“ Engine è°ƒç”¨ä¸Šè¿°çš„ registrarForPlugin æ³¨å†Œ DartNativePlugin æ’ä»¶ã€‚

```
+ (void)registerWithRegistry:(NSObject<FlutterPluginRegistry>*)registry {
  [DartNativePlugin registerWithRegistrar:[registry registrarForPlugin:@"DartNativePlugin"]];
}
```

#### cã€recreatePlatformViewController

- 1ã€è®¾ç½®æ¸²æŸ“ API ä¸º è½¯ä»¶æ¸²æŸ“æ¨¡å¼
- 2ã€é‡æ–°åˆ›å»ºå¹¶é…ç½®ä¸ Flutter å¼•æ“ç›¸å…³çš„å¹³å°è§†å›¾æ§åˆ¶å™¨ FlutterPlatformViewsController

```
- (void)recreatePlatformViewController {
  _renderingApi = flutter::GetRenderingAPIForProcess(FlutterView.forceSoftwareRendering);
  _platformViewsController.reset(new flutter::FlutterPlatformViewsController());
}
```

#### dã€åˆå§‹åŒ– Flutter ä¸åŸç”Ÿå®¿ä¸»é€šä¿¡å¯¹è±¡ _binaryMessenger && _textureRegistry

- 1ã€FlutterBinaryMessengerRelay æ˜¯ Flutter å¼•æ“å†…éƒ¨é€šä¿¡çš„æ¶ˆæ¯ä¼ é€’æœºåˆ¶ï¼Œå®ƒå…è®¸ Flutter æ’ä»¶å’Œå®¿ä¸»åº”ç”¨ä¹‹é—´ç›¸äº’é€šä¿¡ã€‚
- 2ã€FlutterTextureRegistryRelay åˆ™æ˜¯è´Ÿè´£ç®¡ç† Flutter å¼•æ“ä¸­æ‰€æœ‰çº¹ç†çš„æ³¨å†Œè¡¨ã€‚Flutter å¼•æ“ä¸­çš„çº¹ç†ç”¨äºåœ¨ Flutter ä¸­ç»˜åˆ¶å›¾åƒï¼Œæ¯”å¦‚ä»åŸç”Ÿå¹³å°ä¼ é€’åˆ° Flutter ä¸­çš„å›¾åƒæˆ–è§†é¢‘ç­‰èµ„æºã€‚
- 3ã€è¿™ä¸¤è¡Œä»£ç æ˜¯åœ¨åˆå§‹åŒ– FlutterEngine å¯¹è±¡æ—¶åˆ›å»ºäº†è¿™ä¸¤ä¸ªå…³é”®çš„å¯¹è±¡ï¼Œç¡®ä¿äº†åç»­ Flutter æ’ä»¶å’Œ Flutter å¼•æ“ä¹‹é—´çš„é€šä¿¡å’Œçº¹ç†ç®¡ç†èƒ½å¤Ÿæ­£å¸¸è¿›è¡Œã€‚
- 4ã€_connections.reset(new flutter::ConnectionCollection()) åˆå§‹åŒ–è¿æ¥é›†åˆï¼Œä»¥ä¾¿åœ¨ Flutter Engine ä¸­åˆ›å»ºå’Œç®¡ç†é€šä¿¡é€šé“ã€‚

```
_binaryMessenger = [[FlutterBinaryMessengerRelay alloc] initWithParent:self];
_textureRegistry = [[FlutterTextureRegistryRelay alloc] initWithParent:self];
_connections.reset(new flutter::ConnectionCollection());
```

##### Flutter å¼•æ“å®ç°åŸç”Ÿä¸ Flutter é€šä¿¡åè®® FlutterBinaryMessenger

- åŸç”Ÿé€šè¿‡æ³¨å†Œçš„ FlutterMethodChannel çš„ _messenger ï¼ˆFlutterBinaryMessengerï¼‰å¯¹è±¡ç»™ Flutter ç«¯ä¼ æ¶ˆæ¯è°ƒç”¨ sendOnChannel:message æ–¹æ³•

```
/// ç»™ Flutter å‘æ¶ˆæ¯
- (void)invokeMethod:(NSString*)method arguments:(id)arguments {
    FlutterMethodCall* methodCall = [FlutterMethodCall methodCallWithMethodName:method
                                                       arguments:arguments];
    NSData* message = [_codec encodeMethodCall:methodCall];
    [_messenger sendOnChannel:_name message:message];
}

/// æ¥å— Flutter ç«¯çš„æ¶ˆæ¯
- (void)setMethodCallHandler:(FlutterMethodCallHandler)handler {
    if (!handler) {
        if (_connection > 0) {
            [_messenger cleanUpConnection:_connection];
            _connection = 0;
        } else {
            [_messenger setMessageHandlerOnChannel:_name binaryMessageHandler:nil];
        }
        return;
    }
    // Make sure the block captures the codec, not self.
    // `self` might be released before the block, so the block needs to retain the codec to
    // make sure it is not released with `self`
    NSObject<FlutterMethodCodec>* codec = _codec;
    FlutterBinaryMessageHandler messageHandler = ^(NSData* message, FlutterBinaryReply callback) {
        FlutterMethodCall* call = [codec decodeMethodCall:message];
        handler(call, ^(id result) {
            if (result == FlutterMethodNotImplemented) {
                callback(nil);
            } else if ([result isKindOfClass:[FlutterError class]]) {
                callback([codec encodeErrorEnvelope:(FlutterError*)result]);
            } else {
                callback([codec encodeSuccessEnvelope:result]);
            }
        });
    };
    _connection = SetMessageHandler(_messenger, _name, messageHandler, _taskQueue);
}
```

#### fã€æ³¨å†Œ App ç”Ÿå‘½ç›¸å…³é€šçŸ¥

```
  NSNotificationCenter* center = [NSNotificationCenter defaultCenter];
  [center addObserver:self
             selector:@selector(onMemoryWarning:)
                 name:UIApplicationDidReceiveMemoryWarningNotification
               object:nil];

  [center addObserver:self
             selector:@selector(applicationWillEnterForeground:)
                 name:UIApplicationWillEnterForegroundNotification
               object:nil];

  [center addObserver:self
             selector:@selector(applicationDidEnterBackground:)
                 name:UIApplicationDidEnterBackgroundNotification
               object:nil];

  [center addObserver:self
             selector:@selector(onLocaleUpdated:)
                 name:NSCurrentLocaleDidChangeNotification
               object:nil];
```

### 3ã€runWithEntrypoint:

- 1ã€è¿™æ®µä»£ç å®ç°äº†åˆ›å»º Flutter Shell çš„åŠŸèƒ½ã€‚Shell æ˜¯ Flutter çš„æ ¸å¿ƒéƒ¨åˆ†ä¹‹ä¸€ï¼Œç”¨äºå¯åŠ¨ Dart ä»£ç å’Œ Flutter åº”ç”¨ç¨‹åºï¼Œä»è€Œå±•ç¤º Flutter UIã€‚Shell æ˜¯ä¸€ä¸ª C++ åº”ç”¨ç¨‹åºï¼Œå®ƒä¸ Dart ä»£ç ä¹‹é—´ä½¿ç”¨ Embedder API è¿›è¡Œé€šä¿¡ã€‚åœ¨ iOS å¹³å°ä¸Šï¼ŒFlutter çš„ Shell æ˜¯ç”± Objective-C ä»£ç åˆ›å»ºçš„ï¼Œä¸å®¿ä¸»åº”ç”¨ç¨‹åºäº¤äº’ã€‚FlutterEngine ç±»æ˜¯ä¸ Shell äº¤äº’çš„ä¸»è¦æ¥å£ã€‚

- 2ã€Shell çš„ä½œç”¨ï¼Ÿ

    -   aã€Shell æ˜¯ Flutter çš„åº•å±‚æ¡†æ¶ï¼Œå®ƒæä¾›äº†ä¸€ä¸ªè·¨å¹³å°çš„æ¸²æŸ“å¼•æ“ã€è§†å›¾ç³»ç»Ÿå’Œä¸€å¥—åŸºç¡€åº“ã€‚Shell æ˜¯æ„å»º Flutter åº”ç”¨ç¨‹åºçš„åŸºç¡€ï¼Œå®ƒå°†åº”ç”¨ç¨‹åºé€»è¾‘å’Œ Flutter å¼•æ“çš„äº¤äº’å°è£…åœ¨ä¸€èµ·ã€‚
    -   bã€Flutter åº”ç”¨ç¨‹åºé€šå¸¸åŒ…å«ä¸€ä¸ªæˆ–å¤šä¸ª Shellï¼Œæ¯ä¸ª Shell åŒ…å«ä¸€ä¸ªæ¸²æŸ“çº¿ç¨‹å’Œä¸€ä¸ª Dart æ‰§è¡Œä¸Šä¸‹æ–‡ã€‚Shell æ¥æ”¶æ¥è‡ª Dart ä»£ç çš„æŒ‡ä»¤ï¼Œå¹¶å°†å…¶ç¿»è¯‘æˆå›¾å½¢ã€åŠ¨ç”»å’Œå…¶ä»–è§†è§‰æ•ˆæœï¼Œä»¥åŠå“åº”ç”¨æˆ·çš„è¾“å…¥äº‹ä»¶ã€‚Shell è¿˜æä¾›äº†å¯¹ Flutter å¼•æ“çš„è®¿é—®ï¼Œä½¿å¼€å‘è€…å¯ä»¥é…ç½®å¼•æ“å’Œè®¿é—®å®ƒçš„åŠŸèƒ½ã€‚

```
/// åˆ›å»º Flutter Shell å¹¶å¯åŠ¨å¼•æ“
- (BOOL)runWithEntrypoint:(NSString*)entrypoint
               libraryURI:(NSString*)libraryURI
             initialRoute:(NSString*)initialRoute
           entrypointArgs:(NSArray<NSString*>*)entrypointArgs {
  if ([self createShell:entrypoint libraryURI:libraryURI initialRoute:initialRoute]) {
    [self launchEngine:entrypoint libraryURI:libraryURI entrypointArgs:entrypointArgs];
  }
  return _shell != nullptr;
}
```

#### aã€createShell

- è¿™æ®µä»£ç å®ç°äº†åˆ›å»º Flutter Shell çš„åŠŸèƒ½ã€‚Shell æ˜¯ Flutter çš„æ ¸å¿ƒéƒ¨åˆ†ä¹‹ä¸€ï¼Œç”¨äºå¯åŠ¨ Dart ä»£ç å’Œ Flutter åº”ç”¨ç¨‹åºï¼Œä»è€Œå±•ç¤º Flutter UIã€‚Shell æ˜¯ä¸€ä¸ª C++ åº”ç”¨ç¨‹åºï¼Œå®ƒä¸ Dart ä»£ç ä¹‹é—´ä½¿ç”¨ Embedder API è¿›è¡Œé€šä¿¡ã€‚åœ¨ iOS å¹³å°ä¸Šï¼ŒFlutter çš„ Shell æ˜¯ç”± Objective-C ä»£ç åˆ›å»ºçš„ï¼Œä¸å®¿ä¸»åº”ç”¨ç¨‹åºäº¤äº’ã€‚FlutterEngine ç±»æ˜¯ä¸ Shell äº¤äº’çš„ä¸»è¦æ¥å£ã€‚
-   å…·ä½“æ­¥éª¤å¦‚ä¸‹ï¼š

 - 1ã€é¦–å…ˆï¼Œè¯¥å‡½æ•°æ£€æŸ¥ Shell æ˜¯å¦å·²ç»åˆ›å»ºã€‚å¦‚æœå·²ç»åˆ›å»ºï¼Œå‡½æ•°ç›´æ¥è¿”å›ã€‚
 - 2ã€æ¥ä¸‹æ¥ï¼Œè¯¥å‡½æ•°å°†ä¼ å…¥çš„ entrypointã€libraryURI å’Œ initialRoute å‚æ•°ä¿å­˜åˆ°å¯¹è±¡å±æ€§ä¸­ã€‚entrypoint è¡¨ç¤º Dart ä»£ç çš„å…¥å£ç‚¹ï¼ŒlibraryURI è¡¨ç¤ºåº”ç”¨ç¨‹åºçš„ Dart åº“æ–‡ä»¶è·¯å¾„ï¼ŒinitialRoute è¡¨ç¤ºåº”ç”¨ç¨‹åºçš„åˆå§‹è·¯ç”±ã€‚å¦‚æœ initialRoute å‚æ•°ä¸ºç©ºï¼Œè¯¥å‡½æ•°å°è¯•ä» Dart é…ç½®ä¸­è·å–é»˜è®¤çš„è·¯ç”±ã€‚
 - 3ã€ç„¶åï¼Œè¯¥å‡½æ•°è®¾ç½®äº† FlutterView.forceSoftwareRendering å±æ€§ã€‚è¯¥å±æ€§ç”¨äºæ§åˆ¶æ˜¯å¦å¯ç”¨è½¯ä»¶æ¸²æŸ“ï¼Œæ ¹æ® Dart é…ç½®ä¸­çš„ enable_software_rendering å‚æ•°æ¥å†³å®šæ˜¯å¦å¯ç”¨ã€‚
 - 4ã€æ¥ç€ï¼Œè¯¥å‡½æ•°åˆ›å»ºäº†ä¸€ä¸ª Flutter çš„ PlatformData å¯¹è±¡ã€‚PlatformData åŒ…å«äº†è®¸å¤šå¹³å°ç›¸å…³çš„ä¿¡æ¯ï¼Œå¦‚çª—å£å¤§å°ã€æ–‡æœ¬æ–¹å‘ã€ç¼©æ”¾æ¯”ä¾‹ç­‰ã€‚åœ¨ iOS å¹³å°ä¸Šï¼ŒPlatformData åŒ…å«äº†å¹³å°è§†å›¾çš„å¤§å°å’Œä½ç½®ä¿¡æ¯ã€‚
 - 5ã€ç„¶åï¼Œè¯¥å‡½æ•°è®¾ç½®äº†å…¥å£ç‚¹ä¿¡æ¯ï¼Œå¹¶ç”Ÿæˆä¸€ä¸ªçº¿ç¨‹æ ‡ç­¾ç”¨äºæ ‡è¯†è¯¥ FlutterEngine çš„çº¿ç¨‹ã€‚æ¥ç€ï¼Œå‡½æ•°åˆ›å»ºäº†ä¸€ä¸ª ThreadHost å¯¹è±¡ï¼Œè¯¥å¯¹è±¡è¡¨ç¤ºçº¿ç¨‹çš„ä¸»æœºï¼Œå¹¶ä½¿ç”¨ makeThreadHost å‡½æ•°åˆ›å»ºäº†ä¸‰ä¸ªçº¿ç¨‹ï¼šUI çº¿ç¨‹ã€IO çº¿ç¨‹å’Œ Raster çº¿ç¨‹ã€‚è¿™äº›çº¿ç¨‹å°†åœ¨åç»­çš„ Shell åˆ›å»ºè¿‡ç¨‹ä¸­ä½¿ç”¨ã€‚
 - 6ã€ç„¶åï¼Œè¯¥å‡½æ•°åˆ›å»ºäº†ä¸¤ä¸ªå›è°ƒå‡½æ•° on_create_platform_view å’Œ on_create_rasterizerã€‚on_create_platform_view å›è°ƒå‡½æ•°ç”¨äºåˆ›å»ºå¹³å°è§†å›¾ï¼Œè€Œ on_create_rasterizer å›è°ƒå‡½æ•°ç”¨äºåˆ›å»ºå…‰æ …åŒ–å™¨ã€‚è¿™äº›å›è°ƒå‡½æ•°å°†åœ¨åç»­çš„ Shell åˆ›å»ºè¿‡ç¨‹ä¸­ä½¿ç”¨ã€‚
 - 7ã€æ¥ç€ï¼Œè¯¥å‡½æ•°åˆ›å»ºäº†ä¸€ä¸ª TaskRunners å¯¹è±¡ï¼Œè¯¥å¯¹è±¡å°è£…äº†çº¿ç¨‹æ ‡ç­¾ã€å¹³å°ä»»åŠ¡è¿è¡Œå™¨ã€å…‰æ …åŒ–ä»»åŠ¡è¿è¡Œå™¨ã€UI ä»»åŠ¡è¿è¡Œå™¨å’Œ IO ä»»åŠ¡è¿è¡Œå™¨ã€‚è¿™äº›ä»»åŠ¡è¿è¡Œå™¨å°†åœ¨åç»­çš„ Shell åˆ›å»ºè¿‡ç¨‹ä¸­ä½¿ç”¨ã€‚
 - 8ã€æ¥ä¸‹æ¥ï¼Œè¯¥å‡½æ•°æ£€æŸ¥åº”ç”¨ç¨‹åºæ˜¯å¦å¤„äºåå°çŠ¶æ€ã€‚å¦‚æœåº”ç”¨ç¨‹åºå¤„äºåå°çŠ¶æ€ï¼Œåˆ™ç¦ç”¨ GPUï¼Œå¦åˆ™å¯ç”¨ GPUã€‚ç¦ç”¨ GPU å¯ä»¥å‡å°‘ç”µé‡æ¶ˆè€—å’Œçƒ­é‡ï¼Œä»è€Œå»¶é•¿ç”µæ± å¯¿å‘½ã€‚
 - 9ã€ç„¶åï¼Œè¯¥å‡½æ•°è°ƒç”¨ Shell::Create å‡½æ•°åˆ›å»º Shellã€‚Shell::Create å‡½æ•°æ˜¯ä¸€ä¸ªåŒæ­¥å‡½æ•°ï¼Œå®ƒä¼šé˜»å¡å½“å‰çº¿ç¨‹ï¼Œç›´åˆ° Shell åˆ›å»ºå®Œæˆ
 - 10ã€æœ€åï¼Œè°ƒç”¨ setupShell è®¾ç½® Shell , å¹¶ä¸”åˆ¤æ–­å¦‚æœæ˜¯ isProfilerEnabled æ¨¡å¼ä¸‹ï¼Œå¼€å¯æ€§èƒ½åˆ†æ

```
// åˆ›å»º Flutter Shell
- (BOOL)createShell:(NSString*)entrypoint
         libraryURI:(NSString*)libraryURI
       initialRoute:(NSString*)initialRoute {
  if (_shell != nullptr) {
    FML_LOG(WARNING) << "This FlutterEngine was already invoked.";
    return NO;
  }

  self.initialRoute = initialRoute;

  auto settings = [_dartProject.get() settings];
  if (initialRoute != nil) {
    self.initialRoute = initialRoute;
  } else if (settings.route.empty() == false) {
    self.initialRoute = [NSString stringWithCString:settings.route.c_str()
                                           encoding:NSUTF8StringEncoding];
  }

  FlutterView.forceSoftwareRendering = settings.enable_software_rendering;

  auto platformData = [_dartProject.get() defaultPlatformData];

  SetEntryPoint(&settings, entrypoint, libraryURI);

  NSString* threadLabel = [FlutterEngine generateThreadLabel:_labelPrefix];
  _threadHost = std::make_shared<flutter::ThreadHost>();
  *_threadHost = [FlutterEngine makeThreadHost:threadLabel];

  // Lambda captures by pointers to ObjC objects are fine here because the
  // create call is synchronous.
  flutter::Shell::CreateCallback<flutter::PlatformView> on_create_platform_view =
      [self](flutter::Shell& shell) {
        [self recreatePlatformViewController];
        return std::make_unique<flutter::PlatformViewIOS>(
            shell, self->_renderingApi, self->_platformViewsController, shell.GetTaskRunners());
      };

  flutter::Shell::CreateCallback<flutter::Rasterizer> on_create_rasterizer =
      [](flutter::Shell& shell) { return std::make_unique<flutter::Rasterizer>(shell); };

  flutter::TaskRunners task_runners(threadLabel.UTF8String,                          // label
                                    fml::MessageLoop::GetCurrent().GetTaskRunner(),  // platform
                                    _threadHost->raster_thread->GetTaskRunner(),     // raster
                                    _threadHost->ui_thread->GetTaskRunner(),         // ui
                                    _threadHost->io_thread->GetTaskRunner()          // io
  );

  _isGpuDisabled =
      [UIApplication sharedApplication].applicationState == UIApplicationStateBackground;
  // Create the shell. This is a blocking operation.
  std::unique_ptr<flutter::Shell> shell = flutter::Shell::Create(
      /*platform_data=*/platformData,
      /*task_runners=*/task_runners,
      /*settings=*/settings,
      /*on_create_platform_view=*/on_create_platform_view,
      /*on_create_rasterizer=*/on_create_rasterizer,
      /*is_gpu_disabled=*/_isGpuDisabled);

  if (shell == nullptr) {
    FML_LOG(ERROR) << "Could not start a shell FlutterEngine with entrypoint: "
                   << entrypoint.UTF8String;
  } else {
    [self setupShell:std::move(shell)
        withVMServicePublication:settings.enable_vm_service_publication];
    if ([FlutterEngine isProfilerEnabled]) {
      [self startProfiler];
    }
  }

  return _shell != nullptr;
}

```

#### bã€launchEngine

-   é…ç½®å¯åŠ¨ Dart åº”ç”¨ç¨‹åºå¼•æ“çš„ä¸€äº›åˆ—é…ç½®åŒ…æ‹¬

    -  1ã€ä»£ç åº“è·¯å¾„
    -  2ã€å…¥å£ç‚¹ï¼ˆentrypointï¼‰
    -  3ã€åŒ…å«å¯æ‰§è¡Œæ–‡ä»¶çš„è·¯å¾„
    -  4ã€ç¯å¢ƒå˜é‡
    -  5ã€å¯åŠ¨å‚æ•°ç­‰...

```
// å¯åŠ¨å¼•æ“
- (void)launchEngine:(NSString*)entrypoint
libraryURI:(NSString*)libraryOrNil
entrypointArgs:(NSArray<NSString*>*)entrypointArgs {
  // Launch the Dart application with the inferred run configuration.
  self.shell.RunEngine([_dartProject.get() runConfigurationForEntrypoint:entrypoint
                                        libraryOrNil:libraryOrNil
                                        entrypointArgs:entrypointArgs]);
}

- (flutter::RunConfiguration)runConfigurationForEntrypoint:(nullable NSString*)entrypointOrNil
libraryOrNil:(nullable NSString*)dartLibraryOrNil
entrypointArgs:
(nullable NSArray<NSString*>*)entrypointArgs {
    auto config = flutter::RunConfiguration::InferFromSettings(_settings);
    if (dartLibraryOrNil && entrypointOrNil) {
        config.SetEntrypointAndLibrary(std::string([entrypointOrNil UTF8String]),
                               std::string([dartLibraryOrNil UTF8String]));

    } else if (entrypointOrNil) {
        config.SetEntrypoint(std::string([entrypointOrNil UTF8String]));
    }

    if (entrypointArgs.count) {
        std::vector<std::string> cppEntrypointArgs;
        for (NSString* arg in entrypointArgs) {
            cppEntrypointArgs.push_back(std::string([arg UTF8String]));
        }
        config.SetEntrypointArgs(std::move(cppEntrypointArgs));
    }

    return config;
}
```

### 4ã€spawnWithEntrypoint

-   ğŸ“¢ å½“å¼•æ“ç»„é‡Œ self.engines.count > 0 çš„æ—¶å€™ï¼Œåˆ™é€šè¿‡ä¸‹é¢æºç åˆ›å»ºä¸€ä¸ªå¼•æ“ï¼ŒåŒæ—¶å®ƒä¼šå…±äº«ç¬¬ä¸€ä¸ªå¼•æ“çš„éƒ¨åˆ†èµ„æº~
-   è¯¥æ–¹æ³•ç”¨äºåˆ›å»ºä¸€ä¸ªæ–°çš„ FlutterEngine å®ä¾‹ï¼Œè¯¥å®ä¾‹åŒ…å«ä¸€ä¸ª FlutterShell å®ä¾‹å’Œä¸€ä¸ª FlutterDartProject å®ä¾‹ï¼Œå¹¶ä¸”ä¼šé€šè¿‡è°ƒç”¨ FlutterShell å®ä¾‹çš„ Spawn æ–¹æ³•æ¥ç”Ÿæˆä¸€ä¸ªæ–°çš„ Shell å®ä¾‹ï¼Œè¿™ä¸ªæ–°çš„ Shell å®ä¾‹å¯ä»¥ç”¨äºæ§åˆ¶æ–°çš„ Flutter å¼•æ“å®ä¾‹ã€‚
-   å…·ä½“æ­¥éª¤å¦‚ä¸‹ï¼š

 - 1.æ ¹æ® _dartProject è·å–è¿è¡Œé…ç½® configurationã€‚
 - 2.ä»å½“å‰ FlutterShell å®ä¾‹ _shell ä¸­è·å–å¹³å°è§†å›¾ï¼Œå¹¶é€šè¿‡ on_create_platform_view æ–¹æ³•åˆ›å»ºä¸€ä¸ªæ–°çš„ PlatformViewIOS å®ä¾‹ï¼ŒåŒæ—¶é€šè¿‡ on_create_rasterizer æ–¹æ³•åˆ›å»ºä¸€ä¸ªæ–°çš„ Rasterizer å®ä¾‹ã€‚
 - 3.è°ƒç”¨ _shell çš„ Spawn æ–¹æ³•åˆ›å»ºä¸€ä¸ªæ–°çš„ Shell å®ä¾‹ï¼Œå¹¶å°†æ­¥éª¤2ä¸­åˆ›å»ºçš„ PlatformViewIOS å’Œ Rasterizer å®ä¾‹ä¼ é€’ç»™ Spawn æ–¹æ³•ã€‚
 - 4.åˆ›å»ºä¸€ä¸ªæ–°çš„ FlutterEngine å®ä¾‹ resultï¼Œå°† *threadHostã€* profilerã€_profiler_metrics å’Œ _isGpuDisabled å±æ€§å¤åˆ¶åˆ° result ä¸­ã€‚
 - 5.è°ƒç”¨ result çš„ setupShell:withVMServicePublication: æ–¹æ³•ï¼Œå°†æ­¥éª¤3ä¸­åˆ›å»ºçš„ Shell å®ä¾‹ä½œä¸ºå‚æ•°ä¼ é€’ç»™ setupShell æ–¹æ³•ï¼ŒåŒæ—¶ä¼ é€’ä¸€ä¸ª NO å€¼ï¼Œè¡¨ç¤ºä¸å¯ç”¨ VM service publicationã€‚
 - 6.è¿”å› resultã€‚

æ€»ä¹‹ï¼Œè¿™æ®µä»£ç æ˜¯ç”¨äºåˆ›å»ºä¸€ä¸ªæ–°çš„ Flutter å¼•æ“å®ä¾‹çš„ï¼Œå¹¶é€šè¿‡è°ƒç”¨ FlutterShell å®ä¾‹çš„ Spawn æ–¹æ³•æ¥ç”Ÿæˆä¸€ä¸ªæ–°çš„ Shell å®ä¾‹ï¼Œè¿™ä¸ªæ–°çš„ Shell å®ä¾‹å¯ä»¥ç”¨äºæ§åˆ¶æ–°çš„ Flutter å¼•æ“å®ä¾‹ã€‚

```
- (FlutterEngine*)spawnWithEntrypoint:(/*nullable*/ NSString*)entrypoint
                           libraryURI:(/*nullable*/ NSString*)libraryURI
                         initialRoute:(/*nullable*/ NSString*)initialRoute
                       entrypointArgs:(/*nullable*/ NSArray<NSString*>*)entrypointArgs {
  NSAssert(_shell, @"Spawning from an engine without a shell (possibly not run).");
  FlutterEngine* result = [[FlutterEngine alloc] initWithName:_labelPrefix
                                                      project:_dartProject.get()
                                       allowHeadlessExecution:_allowHeadlessExecution];
  flutter::RunConfiguration configuration =
      [_dartProject.get() runConfigurationForEntrypoint:entrypoint
                                           libraryOrNil:libraryURI
                                         entrypointArgs:entrypointArgs];

  fml::WeakPtr<flutter::PlatformView> platform_view = _shell->GetPlatformView();
  FML_DCHECK(platform_view);
  // Static-cast safe since this class always creates PlatformViewIOS instances.
  flutter::PlatformViewIOS* ios_platform_view =
      static_cast<flutter::PlatformViewIOS*>(platform_view.get());
  std::shared_ptr<flutter::IOSContext> context = ios_platform_view->GetIosContext();
  FML_DCHECK(context);

  // Lambda captures by pointers to ObjC objects are fine here because the
  // create call is synchronous.
  flutter::Shell::CreateCallback<flutter::PlatformView> on_create_platform_view =
      [result, context](flutter::Shell& shell) {
        [result recreatePlatformViewController];
        return std::make_unique<flutter::PlatformViewIOS>(
            shell, context, result->_platformViewsController, shell.GetTaskRunners());
      };

  flutter::Shell::CreateCallback<flutter::Rasterizer> on_create_rasterizer =
      [](flutter::Shell& shell) { return std::make_unique<flutter::Rasterizer>(shell); };

  std::string cppInitialRoute;
  if (initialRoute) {
    cppInitialRoute = [initialRoute UTF8String];
  }

  std::unique_ptr<flutter::Shell> shell = _shell->Spawn(
      std::move(configuration), cppInitialRoute, on_create_platform_view, on_create_rasterizer);

  result->_threadHost = _threadHost;
  result->_profiler = _profiler;
  result->_profiler_metrics = _profiler_metrics;
  result->_isGpuDisabled = _isGpuDisabled;
  [result setupShell:std::move(shell) withVMServicePublication:NO];
  return [result autorelease];
}
```

-   é€šè¿‡ spawnWithEntrypoint æºç æˆ‘ä»¬çŸ¥é“äº†é‡‡ç”¨ Group ç®¡ç†å¤šå¼•æ“ï¼Œä¸ºä»€ä¹ˆèƒ½å¤§å¤§é™ä½å†…å­˜åŸå› ï¼šå› ä¸ºå¤šä¸ªå¼•æ“çš„å…¬ç”¨çš„æ˜¯åŒä¸€ä¸ª _shell éƒ¨åˆ†èµ„æº~



## é¢˜å¤–è¯

-   Flutter ç½‘ç»œè¯·æ±‚æ˜¯åœ¨å“ªä¸ªçº¿ç¨‹ä¸­æ‰§è¡Œçš„å—ï¼Ÿä¼šé˜»å¡å¼•å‘ App å¡é¡¿å—ï¼Ÿ

## ![image.png](https://p3-juejin.byteimg.com/tos-cn-i-k3u1fbpfcp/839563def4764ac28b54e828c3f0ffb1~tplv-k3u1fbpfcp-zoom-1.image)

## å‚è€ƒ

-   ã€å®˜æ–¹æ–‡æ¡£ã€‘[åœ¨ iOS åº”ç”¨ä¸­æ·»åŠ  Flutter é¡µé¢](https://flutter.cn/docs/development/add-to-app/ios/add-flutter-screen?tab=engine-uikit-objc-tab)
-   ã€å®˜æ–¹æ–‡æ¡£ã€‘[FlutterEngine](<https://api.flutter.dev/objcdoc/Classes/FlutterEngine.html>)
